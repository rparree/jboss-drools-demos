import fire.*;

dialect "mvel"
rule "When there is a fire turn on Sprinkler"
when
  Fire($room : room)
  $sprinkler : Sprinkler(room == $room, on==false)
then
  modify($sprinkler){
    on = true
  }
  System.out.println("Turned on sprinkler in: "+$room)
end

rule "When there is no fire turn off the sprinkler"
when
  $sprinkler : Sprinkler($room: room, on==true)
  not Fire (room == $room)
then
  modify($sprinkler){
    on = false
  }
  System.out.println("Turned off sprinkler in " + $room)
 end

rule "Raise alarm when there is a fire"
when
  exists Fire()
then
   insertLogical(new AlarmSound());
   System.out.println("Raised the alarm");
end

rule "extinguishe fire after sprinker has been on for 5sec"
  timer (int:5s)
when
    $sprinkler : Sprinkler($room : room , on==true)
    $f : Fire(room == $room)
then
  retract($f)
  System.out.println("Sprinkler did its work in the " + $room)
end

rule "Status output when things are ok"
when
 not AlarmSound()
 not Sprinkler(on == true)
 not Fire()
then
  System.out.println("All is ok")
end



