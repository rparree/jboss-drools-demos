import fire.*;


rule "When there is fire turn on Sprinkler"
when
  Fire($room : room)
  $sprinkler : Sprinkler(room == $room, on==false)
then
  modify($sprinkler){
    setOn(true)
  };
  System.out.println("Turned on sprinkler in room: "+$room.getName());
end

rule "When fire has been extinguished turn off the sprinkler"
when
  $sprinkler : Sprinkler($room: room, on==true)
  not Fire (room == $room)
then
  modify($sprinkler){setOn(false)};
  System.out.println("Turned off sprinkler in room: "+$room.getName());
 end

rule "Raise alarm when there is a fire"
when
  exists Fire()
then
   insertLogical(new Alarm());
   System.out.println("Raised the alarm");
end

rule "extinguishe fire after sprinker has been on for 5sec"
  timer (int:5s)
when
    $sprinkler : Sprinkler($room : room , on==true)
    $f : Fire(room == $room)
then
  System.out.println("Sprinker did its work");
  retract($f)
end

rule "Status output when things are ok"
when
 not Alarm()
 not Sprinkler(on == true)
then
  System.out.println("All is ok");
end



